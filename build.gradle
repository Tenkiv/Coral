buildscript {
    ext.kotlin_version = '1.2.51'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true
        }
    }

    repositories {
        mavenCentral()
    }
}

configure(project(':coral-common')) {
    apply plugin: 'kotlin-platform-common'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    }
}

configure(project(':coral-jvm')) {
    apply plugin: 'kotlin-platform-jvm'
    apply plugin: 'jacoco'

    compileKotlin.kotlinOptions.jvmTarget = "1.8"
    compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

    test {
        useJUnitPlatform()
    }

    dependencies {
        expectedBy project(':coral-common')

        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

        testCompile 'io.kotlintest:kotlintest-runner-junit5:3.1.0'
        testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    }

    jacoco {
        toolVersion = '0.8.1'
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    test.finalizedBy jacocoTestReport
}
